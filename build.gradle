plugins {
	id 'dev.kikugie.stonecutter'
	id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
	id 'maven-publish'
	id 'com.modrinth.minotaur' version '2.+'
}

version = "${mod_version}+mc${stonecutter.current.version}"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "Aqk3l0pH"
	versionNumber = "${mod_version}+mc${stonecutter.current.version}"
	versionName = "Memory Leak ${mod_version} for ${stonecutter.current.version}"
	versionType = "release"
	uploadFile = remapJar
	gameVersions = [stonecutter.current.version]
	loaders = ["fabric"]
	changelog = file("CHANGELOG.md").exists() ? file("CHANGELOG.md").text : "No changelog provided."

	syncBodyFrom = rootProject.file("README.md").text

	dependencies {
		required.project "fabric-api"
	}
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)